📗 Type containing all methods for solving the first day of Advent of Code 2024. 📗
🐇 🕵️ 🍇
  📗
    Compute the calibration value for one input line.

    Rethrows errors in case the line doesn't contain any numbers.
  📗
  🐇❓ 💡 line🔡 ➡️ 🔢 🚧🚧 🍇
    ↩️ 🔺 ⏮️🐇🕵️ line ❓ ✖️ 10 ➕ 🔺 ⏭️🐇🕵️ line ❓
  🍉

  📗
    Returns the first number appearing in a string.

    Throws an error if no number was found in the string.
  📗
  🐇❓ ⏮️ str🔡 ➡️ 🔢 🚧🚧 🍇
    🔂 i 🆕⏩ 0 📐str❗️➖1 ❗️ 🍇 💭 Go through substrings from left to right.
      🔪 str i 📐str❗️➖i ❗️ ➡️ substr
      🔞🐇🕵️ substr ❓ ➡️ number
      ↪️ ❎ number 🙌 🤷‍♀️ ❗️ 🍇 💭 If a number was found, return it.
        ↩️ 🍺 number
      🍉
    🍉
    🚨 🆕🚧 🔤No number found in input.🔤❗️
  🍉

  📗
    Returns the last number appearing in a string.
    
    Throws an error if no number was found in the string.
  📗
  🐇❓ ⏭️ str🔡 ➡️ 🔢 🚧🚧 🍇
    🔂 i 🆕⏩ 📐str❗️➖2 -1 ❗️ 🍇 💭 Go through substrings from right to left.
      🔪 str i 📐str❗️➖i ❗️ ➡️ substr
      🔞🐇🕵️ substr ❓ ➡️ number
      ↪️ ❎ number 🙌 🤷‍♀️ ❗️ 🍇 💭 If a number was found, return it.
        ↩️ 🍺 number
      🍉
    🍉
    🚨 🆕🚧 🔤No number found in input.🔤❗️
  🍉

  📗
    Return the integer starting at the beginning of the input string. If the string
    doesn't start with an integer, return an empty optional.
  📗
  🐇❓ 🔞 str🔡 ➡️ 🍬🔢 🍇
    🍿
      🔤one🔤 ➡️ 1
      🔤two🔤 ➡️ 2
      🔤three🔤 ➡️ 3
      🔤four🔤 ➡️ 4
      🔤five🔤 ➡️ 5
      🔤six🔤 ➡️ 6
      🔤seven🔤 ➡️ 7
      🔤eight🔤 ➡️ 8
      🔤nine🔤 ➡️ 9
    🍆 ➡️ numbers
    🔂 number 🐙 numbers ❗️ 🍇 💭 Iterate thorugh all keys of the map.
      ↪️ 🎼 str number ❗️ 🍇 💭 If the string starts with the key, return.
        ↩️ 🐽 numbers number ❗️ 💭 Convert the word into the corresponding integer.
      🍉
    🍉
    ↩️ 🔢 🐽 🎶 str ❗️ 0 ❗️ 10 ❗️
  🍉
🍉

📗 Main method. Read all input lines and call the 🕵️ type for each line. 📗
🏁 🍇
  🆕 🔡▶️👂🏼❗️ ➡️ 🖍🆕line 💭 Read the first line of the input.
  0 ➡️ 🖍🆕sum
  🔁 📐line❗️ ▶️ 0 🍇 💭 While the line read is not empty.
    sum ⬅️➕ 🍺 💡🐇🕵️ line ❓ 💭 Call the 🕵️ type and sum up the results.
    🆕 🔡▶️👂🏼❗️ ➡️ 🖍line 💭 Read the next line of the input.
  🍉
  😀 🔡sum❗️ ❗️
🍉
